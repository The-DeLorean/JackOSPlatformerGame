// This file is part of JackOSPlatformerGame
// by Dorian Quimby
/**
 *  A child class of GameObject, which contains the functionality of a Player
 *  Players can move, collect KeyBlocks, die when the touch spikeblocks, and reach the VictoryBlock
 */
class Player {
    field int livesRemaining; //keeps track of deaths, will work with spikeblock
    static int speed; //
    field boolean win;
    field boolean death;
    field boolean key;
    field int xpos, ypos, prevXPos, prevYPos;

    constructor Player new(int xin, int yin) {
        //Compose a child member data into parent game object
        let xpos = xin;
        let ypos = yin;
        let speed = 5;
        return this;
    }

    // Players can move
    method void move(int dx, int dy, GameMap map) {
        var boolean check;
        let prevXPos = xpos;
        let prevYPos = ypos;
        let xpos = (xpos + dx);
        let check = collides(map);
        if(check = true){
            let xpos = prevXPos;
        }
        let ypos = (ypos + dy);
        //updating the player block each time its moved
        let check = collides(map);
        if(check = true){
            let ypos = prevYPos;
        }
        
        do draw();
        return;
    }


    // Renders the player
    method void draw() {
        do Screen.setColor(false);
        do Screen.drawRectangle(prevXPos, prevYPos, prevXPos + 7, prevYPos + 7);
        do Screen.setColor(true);
        do Screen.drawRectangle(xpos, ypos, xpos + 7, ypos + 7);
        return;
    }

    //method to check if the player is on the ground
    method boolean isOnGround(GameMap map){
        //creating a check variable
        var boolean check;
        //Intailiing check to false
        let check =false;
        //incrementing the y value after move
        let ypos = ypos + 1;
        //checking with the map
        let check = collides(map);
        //decrementing the y value after move to move player back
        let ypos = ypos - 1;
        //Cecking whether to see if the collision was true or not returning the respective varaiable
        return check;
    }

    //Checking to see if the player hits the ceiling
    method boolean hitsCieling(GameMap map){
        //creating a check variable
        var boolean check;
        //Initalizing check to false
        let check = false;
        //decrementing the y to check 1 ahead
        let ypos = ypos - 1;
        //checking with the map
        let check = collides(map);
        //Incrementing the y value after move to move player back
        let ypos = ypos + 1;
        //Cecking whether to see if the collision was true or not returning the respective varaiable
        return check;
    }

    method boolean collides(GameMap map){
        //variables to count loop iterations
        var int i;
        var int numberOfBlocks;
        //Variable to hold the collision flag
        var boolean collision;
        var Array blocks;
        //variable to hold the gameObject from the array
        var GameObject gameObj;

        //initalizing loop variables.
        //let rowSize = 30;
        //let columnSize = 64;
        let i=0;
        let blocks = map.getGameBlocks();
        let numberOfBlocks = map.getNumOfBlocks();

        while(i < numberOfBlocks){
            let gameObj = blocks[i];
            let collision = overlap(gameObj);
            if(collision = true)
            {
                return true;
            }
            let i = i + 1 ;
        }
        //If we don't overlap return false no collision
        return false;
    }

    // Collision logic for all GameObjects.  Hitbox is 8x8, regardless of whether there is something rendered.  E.g. spikes are really square.
    method boolean overlap(GameObject o){
        if ((xpos < (o.getX() + 8)) & ((xpos + 8 ) > o.getX()) & (ypos < (o.getY() +8)) & ((ypos + 8) > o.getY()))
        {
            return true;
        }
        else{
            return false;
        }
    }

    //Accessor for the win boolean variable
    method boolean won()
    {
        return win;
    }
    //Accessor for the win boolean variable
    method boolean died()
    {
        return death;
    }
    //Accessor for the win boolean variable
    method boolean hasKey()
    {
        return key;
    }

    //a method to create a new life
    method void newLife(){
        let death = false;
        return;
    }

    method String instanceof()
    {
        return "Player";
    }
}